package Equal where

def Equal (A: Type) (a b: A) : Type =
  @self ∀
  (0 P    : ∀ (b: A) (Equal A a b) -> Type)
  (& refl : P a (data λ P refl => refl))
  -> P b self

def refl (0 A: Type) (a : A) : Equal A a a = data λ P refl => refl

def sym (0 A: Type) (a b: A) (e: Equal A a b) : Equal A b a
  = (case e) (λ b e => Equal A b a) (refl A a)

def trans (0 A: Type) (a b c: A) (ab: Equal A a b) (bc: Equal A b c): Equal A a c
  = (case bc) (λ b' _ => Equal A a b') ab

def cong (0 A B: Type) (a b: A) (f: ∀ A -> B) (e: Equal A a b): Equal B (f a) (f b)
  = (case e) (λ b' _ => Equal B (f a) (f b')) (refl B (f a))

def cast (0 A: Type) (a b: A) (0 P: ∀ A -> Type) (e: Equal A a b) (x: P a): P b
  = (case e) (λ b' _ => P b') x
